apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'com.bmuschko.nexus'

group = "cl.daplay"
description = "An idiomatic Java API for Surbtc REST services."
version = "2.1.0"

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

javadoc.doLast {
    // contents of "gtag.js" file will be inserted after every head
    String gtag = file("gtag.js").text

    // for each html
    def files = fileTree(destinationDir).include('**/*.html')
    files.each { File file ->
        def content = file.text

        def indexOfHead = content.indexOf("<head>");
        if (indexOfHead) {
            println file
            // gets a builder to update content
            StringBuilder builder = new StringBuilder(content)

            // determines post of insertion (just after head element)
            def insertIndex = indexOfHead + "<head>".length()

            // insertion taking place
            builder.insert(insertIndex, gtag);

            // replace file
            file.text = builder.toString()
        }
    }

}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.9'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.8.9'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.8.9'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    testLogging.showStandardStreams = true

    final boolean runIntegration = project.hasProperty('jsurbtc.secret') && project.hasProperty('jsurbtc.key')
    if (!runIntegration) {
        exclude { it.name.endsWith('_IT.class') }
    }
}

jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

modifyPom {
    project {

        name project.name
        description project.description
        url 'https://github.com/daplay/jsurbtc'

        scm {
            url 'https://github.com/daplay/jsurbtc'
            connection 'scm:https://github.com/daplay/jsurbtc.git'
            developerConnection 'scm:git://github.com/daplay/jsurbtc.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'daplay'
                name 'Daniel Meneses'
                email 'daplay@gmail.com'
            }
        }
    }
}
